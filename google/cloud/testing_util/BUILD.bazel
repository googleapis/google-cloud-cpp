# Copyright 2019 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# The targets in this package are intended for testing our own libraries, and
# are not intended for public usage.
package(default_visibility = ["//:__subpackages__"])

licenses(["notice"])  # Apache 2.0

load(":google_cloud_cpp_testing.bzl", "google_cloud_cpp_testing_hdrs", "google_cloud_cpp_testing_srcs")

cc_library(
    name = "google_cloud_cpp_testing_private",
    srcs = google_cloud_cpp_testing_srcs,
    hdrs = google_cloud_cpp_testing_hdrs,
    defines = select({
        "@platforms//os:linux": [
            "GOOGLE_CLOUD_CPP_HAVE_GETRUSAGE",
            "GOOGLE_CLOUD_CPP_HAVE_RUSAGE_THREAD",
        ],
        "@platforms//os:macos": [
            "GOOGLE_CLOUD_CPP_HAVE_GETRUSAGE",
        ],
        "//conditions:default": [],
    }),
    deps = [
        "//:common",
        "@com_google_absl//absl/debugging:failure_signal_handler",
        "@com_google_absl//absl/debugging:symbolize",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "google_cloud_cpp_testing",
    deprecation = """
    This target is deprecated and will be removed on or after 2023-04-01. More
    info: https://github.com/googleapis/google-cloud-cpp/issues/3701
    """,
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [":google_cloud_cpp_testing_private"],
)

load(":google_cloud_cpp_testing_unit_tests.bzl", "google_cloud_cpp_testing_unit_tests")

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    srcs = [test],
    deps = [
        ":google_cloud_cpp_testing",
        "//:common",
        "@com_google_googletest//:gtest_main",
    ],
) for test in google_cloud_cpp_testing_unit_tests]

load(":google_cloud_cpp_testing_grpc.bzl", "google_cloud_cpp_testing_grpc_hdrs", "google_cloud_cpp_testing_grpc_srcs")

cc_library(
    name = "google_cloud_cpp_testing_grpc_private",
    srcs = google_cloud_cpp_testing_grpc_srcs,
    hdrs = google_cloud_cpp_testing_grpc_hdrs,
    deps = [
        "//:common",
        "//:grpc_utils",
        "@com_github_grpc_grpc//:grpc++",
        "@com_google_googleapis//:googleapis_system_includes",
        "@com_google_googleapis//google/api:annotations_cc_proto",
        "@com_google_googletest//:gtest_main",
        "@com_google_protobuf//:protobuf",
    ],
)

cc_library(
    name = "google_cloud_cpp_testing_grpc",
    deprecation = """
    This target is deprecated and will be removed on or after 2023-04-01. More
    info: https://github.com/googleapis/google-cloud-cpp/issues/3701
    """,
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [":google_cloud_cpp_testing_grpc_private"],
)

load(":google_cloud_cpp_testing_grpc_unit_tests.bzl", "google_cloud_cpp_testing_grpc_unit_tests")

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    srcs = [test],
    deps = [
        ":google_cloud_cpp_testing_grpc",
        "//:common",
        "@com_google_googletest//:gtest_main",
        "@com_google_protobuf//:protobuf",
    ],
) for test in google_cloud_cpp_testing_grpc_unit_tests]

load(":google_cloud_cpp_testing_rest.bzl", "google_cloud_cpp_testing_rest_hdrs", "google_cloud_cpp_testing_rest_srcs")

cc_library(
    name = "google_cloud_cpp_testing_rest_private",
    srcs = google_cloud_cpp_testing_rest_srcs,
    hdrs = google_cloud_cpp_testing_rest_hdrs,
    deps = [
        "//google/cloud:google_cloud_cpp_rest_internal",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "google_cloud_cpp_testing_rest",
    deprecation = """
    This target is deprecated and will be removed on or after 2023-04-01. More
    info: https://github.com/googleapis/google-cloud-cpp/issues/3701
    """,
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [":google_cloud_cpp_testing_rest_private"],
)

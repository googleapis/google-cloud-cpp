# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

load(":google_cloud_cpp_pubsub.bzl", "google_cloud_cpp_pubsub_hdrs", "google_cloud_cpp_pubsub_srcs")

cc_library(
    name = "google_cloud_cpp_pubsub",
    srcs = google_cloud_cpp_pubsub_srcs,
    hdrs = google_cloud_cpp_pubsub_hdrs,
    visibility = [
        ":__subpackages__",
        "//:__pkg__",
    ],
    deps = [
        "//:common",
        "//:grpc_utils",
        "@com_google_googleapis//google/pubsub/v1:pubsub_cc_grpc",
        # Do not sort: grpc++ must come last
        "@com_github_grpc_grpc//:grpc++",
    ],
)

load(":pubsub_client_testing.bzl", "pubsub_client_testing_hdrs", "pubsub_client_testing_srcs")

cc_library(
    name = "pubsub_client_testing_private",
    testonly = True,
    srcs = pubsub_client_testing_srcs,
    hdrs = pubsub_client_testing_hdrs,
    visibility = [
        ":__subpackages__",
    ],
    deps = [
        ":google_cloud_cpp_pubsub",
        "//:common",
        "//:grpc_utils",
        "@com_google_googletest//:gtest",
    ],
)

# TODO(#3701): Delete this target after 2023-04-01.
cc_library(
    name = "pubsub_client_testing",
    testonly = True,
    deprecation = """
    This target is deprecated and will be removed on or after 2023-04-01. More
    info: https://github.com/googleapis/google-cloud-cpp/issues/3701
    """,
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [":pubsub_client_testing_private"],
)

load(":google_cloud_cpp_pubsub_mocks.bzl", "google_cloud_cpp_pubsub_mocks_hdrs")

cc_library(
    name = "google_cloud_cpp_pubsub_mocks",
    testonly = True,
    srcs = [],
    hdrs = google_cloud_cpp_pubsub_mocks_hdrs,
    visibility = [
        ":__subpackages__",
        "//:__pkg__",
    ],
    deps = [
        ":google_cloud_cpp_pubsub",
        "//:common",
        "//:grpc_utils",
        "@com_google_googleapis//google/pubsub/v1:pubsub_cc_grpc",
    ],
)

load(":pubsub_client_unit_tests.bzl", "pubsub_client_unit_tests")

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    srcs = [test],
    deps = [
        ":google_cloud_cpp_pubsub",
        ":google_cloud_cpp_pubsub_mocks",
        ":pubsub_client_testing",
        "//:common",
        "//google/cloud/testing_util:google_cloud_cpp_testing_grpc_private",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
        "@com_google_absl//absl/strings:str_format",
        "@com_google_googletest//:gtest_main",
    ],
) for test in pubsub_client_unit_tests]

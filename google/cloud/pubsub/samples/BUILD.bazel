# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""Examples for the Cloud Pub/Sub C++ client library."""

load("@com_google_protobuf//bazel:cc_proto_library.bzl", "cc_proto_library")
load("@com_google_protobuf//bazel:proto_library.bzl", "proto_library")
load(":pubsub_client_integration_samples.bzl", "pubsub_client_integration_samples")
load(":pubsub_client_unit_samples.bzl", "pubsub_client_unit_samples")
load(":pubsub_samples_common.bzl", "pubsub_samples_common_hdrs", "pubsub_samples_common_srcs")
load(":pubsub_samples_unit_tests.bzl", "pubsub_samples_unit_tests")

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

cc_library(
    name = "pubsub_samples_common",
    testonly = True,
    srcs = pubsub_samples_common_srcs,
    hdrs = pubsub_samples_common_hdrs,
    data = glob(["testdata/*"]),
    deps = [
        "//:common",
        "//:pubsub",
        "//google/cloud/pubsub:pubsub_client_testing_private",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
    ],
)

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    srcs = [test],
    deps = [
        ":pubsub_samples_common",
        "//:common",
        "//:pubsub",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
        "@com_google_googletest//:gtest_main",
    ],
) for test in pubsub_samples_unit_tests]

proto_library(
    name = "samples_proto",
    srcs = [
        "testdata/schema.proto",
    ],
    visibility = ["//:__pkg__"],
)

cc_proto_library(
    name = "samples_cc_proto",
    visibility = ["//:__pkg__"],
    deps = [":samples_proto"],
)

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    timeout = "long",
    srcs = [test],
    copts = ["-I$(GENDIR)"],
    tags = [
        "integration-test",
    ],
    deps = [
        ":pubsub_samples_common",
        ":samples_cc_proto",
        "//:common",
        "//:pubsub",
    ] + (["//:iam"] if test == "iam_samples.cc" else []),
) for test in pubsub_client_integration_samples]

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    srcs = [test],
    deps = [
        ":pubsub_samples_common",
        "//:common",
        "//:pubsub",
        "//:pubsub_mocks",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
        "@com_google_googletest//:gtest_main",
    ],
) for test in pubsub_client_unit_samples]

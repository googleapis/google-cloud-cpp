# Copyright 2023 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

load("@rules_proto//proto:defs.bzl", "proto_library")
load(
    "@com_google_googleapis_imports//:imports.bzl",
    "cc_proto_library",
)
load(":service_dirs.bzl", "operation_service_dirs", "service_dirs")

# Internal protos target
filegroup(
    name = "internal_protos",
    srcs = glob(["v1/internal/*.proto"]),
)

proto_library(
    name = "google_cloud_cpp_compute_internal_proto",
    srcs = [":internal_protos"],
    visibility = ["//:__pkg__"],
    deps = [
        "@com_google_googleapis//google/cloud:extended_operations_proto",
        "@com_google_protobuf//:any_proto",
    ],
)

cc_proto_library(
    name = "google_cloud_cpp_compute_internal_cc_proto",
    deps = [":google_cloud_cpp_compute_internal_proto"],
)

# Service proto targets
[[
    filegroup(
        name = service.replace("/v1/", "_proto_srcs"),
        srcs = glob([service + "*.proto"]),
    ),
    proto_library(
        name = "google_cloud_cpp_compute_" + service.replace("/v1/", "_proto"),
        srcs = [":" + service.replace("/v1/", "_proto_srcs")],
        visibility = ["//:__pkg__"],
        deps = [
            ":google_cloud_cpp_compute_internal_proto",
            "@com_google_googleapis//google/api:annotations_proto",
            "@com_google_googleapis//google/api:client_proto",
            "@com_google_googleapis//google/api:field_behavior_proto",
            "@com_google_googleapis//google/api:http_proto",
            "@com_google_googleapis//google/api:resource_proto",
            "@com_google_googleapis//google/api:routing_proto",
            "@com_google_googleapis//google/cloud:extended_operations_proto",
            "@com_google_protobuf//:empty_proto",
        ],
    ),
    cc_proto_library(
        name = "google_cloud_cpp_compute_" + service.replace("/v1/", "_cc_proto"),
        deps = [":google_cloud_cpp_compute_" + service.replace("/v1/", "_proto")],
    ),
] for service in service_dirs + operation_service_dirs]

# Service C++ files and mocks targets
[[
    filegroup(
        name = service.replace("/v1/", "_srcs"),
        srcs = glob([service + "*.cc"]) + glob([service + "internal/*.cc"]),
    ),
    filegroup(
        name = service.replace("/v1/", "_hdrs"),
        srcs = glob([service + "*.h"]) + glob([service + "internal/*.h"]),
    ),
    filegroup(
        name = service.replace("/v1/", "_public_hdrs"),
        srcs = glob([service + "*.h"]),
        visibility = ["//:__pkg__"],
    ),
    filegroup(
        name = service.replace("/v1/", "_mock_hdrs"),
        srcs = glob([service + "mocks/*.h"]),
        visibility = ["//:__pkg__"],
    ),
    cc_library(
        name = "google_cloud_cpp_compute_" + service.replace("/v1/", "_mocks"),
        hdrs = [":" + service.replace("/v1/", "_mock_hdrs")],
        visibility = ["//:__pkg__"],
        deps = [
            ":google_cloud_cpp_compute_" + service.replace("/v1/", ""),
            "@com_google_googletest//:gtest",
        ],
    ),
] for service in service_dirs + operation_service_dirs]

# Operation services
[cc_library(
    name = "google_cloud_cpp_compute_" + service.replace("/v1/", ""),
    srcs = [":" + service.replace("/v1/", "_srcs")],
    hdrs = [":" + service.replace("/v1/", "_hdrs")],
    copts = [
        "-I$(BINDIR)",
    ],
    visibility = ["//:__pkg__"],
    deps = [
        ":google_cloud_cpp_compute_" + service.replace("/v1/", "") + "_cc_proto",
        "//:common",
        "//:grpc_utils",
        "//google/cloud:google_cloud_cpp_rest_internal",
        "//google/cloud:google_cloud_cpp_rest_protobuf_internal",
    ],
) for service in operation_service_dirs]

# Services
[cc_library(
    name = "google_cloud_cpp_compute_" + service.replace("/v1/", ""),
    srcs = [":" + service.replace("/v1/", "_srcs")],
    hdrs = [":" + service.replace("/v1/", "_hdrs")],
    copts = [
        "-I$(BINDIR)",
    ],
    visibility = ["//:__pkg__"],
    deps = [
        ":google_cloud_cpp_compute_global_operations",
        ":google_cloud_cpp_compute_global_organization_operations",
        ":google_cloud_cpp_compute_region_operations",
        ":google_cloud_cpp_compute_zone_operations",
        ":google_cloud_cpp_compute_" + service.replace("/v1/", "") + "_cc_proto",
        "//:common",
        "//:grpc_utils",
        "//google/cloud:google_cloud_cpp_rest_internal",
        "//google/cloud:google_cloud_cpp_rest_protobuf_internal",
    ],
) for service in service_dirs]

[cc_test(
    name = sample.replace("/", "_").replace(".cc", ""),
    srcs = [sample],
    copts = [
        "-I$(BINDIR)",
    ],
    tags = ["integration-test"],
    deps = [
        ":google_cloud_cpp_compute_" + sample[0:sample.find("/v")],
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
    ],
) for sample in glob([d + "samples/*.cc" for d in service_dirs])]

# Copyright 2022 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

service_dirs = [
    "",
    "v1/",
    "v2/",
]

code_glob = [d + i + f for d in service_dirs for i in ["", "internal/"] for f in ["*.h", "*.cc"]]

sources_glob = [d + "internal/*_sources.cc" for d in service_dirs]

filegroup(
    name = "srcs",
    srcs = glob(sources_glob),
)

filegroup(
    name = "hdrs",
    srcs = glob(include = code_glob, exclude = sources_glob),
)

filegroup(
    name = "public_hdrs",
    srcs = glob([d + "*.h" for d in service_dirs]),
    visibility = ["//:__pkg__"],
)

filegroup(
    name = "mocks",
    srcs = glob([d + "mocks/*.h" for d in service_dirs]),
    visibility = ["//:__pkg__"],
)

cc_library(
    name = "google_cloud_cpp_securitycenter",
    srcs = [":srcs"],
    hdrs = [":hdrs"],
    # Cannot compile on Windows - the protos have enum values that
    # conflict with some system macros.
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//:__pkg__"],
    deps = [
        "//:common",
        "//:grpc_utils",
        "@com_google_googleapis//google/cloud/securitycenter/v1:securitycenter_cc_grpc",
        "@com_google_googleapis//google/cloud/securitycenter/v2:securitycenter_cc_grpc",
    ],
)

cc_library(
    name = "google_cloud_cpp_securitycenter_mocks",
    hdrs = [":mocks"],
    # Cannot compile on Windows - the protos have enum values that
    # conflict with some system macros.
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//:__pkg__"],
    deps = [
        ":google_cloud_cpp_securitycenter",
        "@com_google_googletest//:gtest",
    ],
)

[cc_test(
    name = sample.replace("/", "_").replace(".cc", ""),
    srcs = [sample],
    tags = ["integration-test"],
    deps = [
        "//:securitycenter",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
    ],
) for sample in glob([d + "samples/*.cc" for d in service_dirs])]

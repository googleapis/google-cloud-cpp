# Copyright 2021 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load(":google_cloud_cpp_pubsublite.bzl", "google_cloud_cpp_pubsublite_hdrs", "google_cloud_cpp_pubsublite_srcs")
load(":google_cloud_cpp_pubsublite_mocks.bzl", "google_cloud_cpp_pubsublite_mocks_hdrs")
load(":pubsublite_testing.bzl", "pubsublite_testing_hdrs", "pubsublite_testing_srcs")
load(":pubsublite_unit_tests.bzl", "pubsublite_unit_tests")

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

filegroup(
    name = "public_hdrs",
    srcs = [h for h in google_cloud_cpp_pubsublite_hdrs if not h.startswith("internal/")],
    visibility = ["//:__pkg__"],
)

cc_library(
    name = "google_cloud_cpp_pubsublite",
    srcs = google_cloud_cpp_pubsublite_srcs,
    hdrs = google_cloud_cpp_pubsublite_hdrs,
    visibility = ["//:__pkg__"],
    deps = [
        "//:common",
        "//:grpc_utils",
        "//:pubsub",
        "@googleapis//google/cloud/pubsublite/v1:pubsublite_cc_grpc",
    ],
)

filegroup(
    name = "mocks",
    srcs = [h for h in google_cloud_cpp_pubsublite_mocks_hdrs if not h.startswith("internal/")],
    visibility = ["//:__pkg__"],
)

cc_library(
    name = "google_cloud_cpp_pubsublite_mocks",
    testonly = True,
    srcs = [],
    hdrs = google_cloud_cpp_pubsublite_mocks_hdrs,
    visibility = ["//:__pkg__"],
    deps = [
        ":google_cloud_cpp_pubsublite",
        "@googletest//:gtest",
    ],
)

cc_library(
    name = "pubsublite_testing",
    testonly = True,
    srcs = pubsublite_testing_srcs,
    hdrs = pubsublite_testing_hdrs,
    visibility = [
        ":__subpackages__",
    ],
    deps = [
        ":google_cloud_cpp_pubsublite",
        "//:common",
        "//:grpc_utils",
    ],
)

[
    cc_test(
        name = test.replace("/", "_").replace(".cc", ""),
        srcs = [test],
        deps = [
            ":google_cloud_cpp_pubsublite",
            ":google_cloud_cpp_pubsublite_mocks",
            ":pubsublite_testing",
            "//:common",
            "//:mocks",
            "//google/cloud/testing_util:google_cloud_cpp_testing_grpc_private",
            "//google/cloud/testing_util:google_cloud_cpp_testing_private",
            "@abseil-cpp//absl/memory",
            "@googletest//:gtest_main",
        ],
    )
    for test in pubsublite_unit_tests
]

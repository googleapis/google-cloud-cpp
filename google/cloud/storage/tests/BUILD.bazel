# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load(
    ":storage_client_integration_tests.bzl",
    "storage_client_integration_tests",
)

package(default_visibility = ["//visibility:private"])

licenses(["notice"])  # Apache 2.0

VARIATIONS = {
    "default": {"GOOGLE_CLOUD_CPP_STORAGE_GRPC_CONFIG": "none"},
    "grpc-metadata": {"GOOGLE_CLOUD_CPP_STORAGE_GRPC_CONFIG": "metadata"},
}

[cc_test(
    name = test.replace("/", "_").replace(".cc", "") + "-" + v_label,
    timeout = "long",
    srcs = [test],
    env = v_env,
    linkopts = select({
        "@platforms//os:windows": [],
        "//conditions:default": [
            "-lpthread",
            "-ldl",
        ],
    }),
    tags = [
        "integration-test",
        "integration-test-" + v_label,
    ] + (["integration-test-gha"] if test.startswith("smoke_test") else []),
    deps = [
        "//:common",
        "//:storage",
        "//google/cloud/storage:storage_client_testing",
        "//google/cloud/testing_util:google_cloud_cpp_testing_grpc_private",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
        "//protos/google/cloud/storage/tests:storage_conformance_tests_cc_proto",
        "@abseil-cpp//absl/strings",
        "@googletest//:gtest_main",
    ],
) for test in storage_client_integration_tests for v_label, v_env in VARIATIONS.items()]

[cc_test(
    name = test.replace("/", "_").replace(".cc", ""),
    timeout = "long",
    srcs = [test],
    linkopts = select({
        "@platforms//os:windows": [],
        "//conditions:default": [
            "-lpthread",
            "-ldl",
        ],
    }),
    tags = [
        "integration-test",
    ],
    deps = [
        "//:common",
        "//:experimental-universe_domain",
        "//:storage",
        "//google/cloud/storage:storage_client_testing",
        "//google/cloud/testing_util:google_cloud_cpp_testing_grpc_private",
        "//google/cloud/testing_util:google_cloud_cpp_testing_private",
        "//protos/google/cloud/storage/tests:storage_conformance_tests_cc_proto",
        "@abseil-cpp//absl/strings",
        "@googletest//:gtest_main",
    ],
) for test in ["universe_domain_integration_test.cc"]]

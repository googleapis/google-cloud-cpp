# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("//bazel:library_names.bzl", "hdrs_filegroup_name", "library_dir_name", "mocks_filegroup_name")
load(":libraries.bzl", "GOOGLE_CLOUD_CPP_EXPERIMENTAL_LIBRARIES", "GOOGLE_CLOUD_CPP_GA_LIBRARIES", "GOOGLE_CLOUD_CPP_TRANSITION_LIBRARIES")

package(default_visibility = ["//visibility:public"])

licenses(["notice"])  # Apache 2.0

exports_files([
    "LICENSE",
])

EXPERIMENTAL_LIBRARIES = GOOGLE_CLOUD_CPP_EXPERIMENTAL_LIBRARIES

TRANSITION_LIBRARIES = GOOGLE_CLOUD_CPP_TRANSITION_LIBRARIES

GA_LIBRARIES = GOOGLE_CLOUD_CPP_GA_LIBRARIES

AD_HOC_LIBRARIES = ["storage_grpc"]

NO_MOCK_LIBRARIES = ["oauth2"] + AD_HOC_LIBRARIES

[cc_library(
    name = "experimental-{library}".format(library = library),
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = hdrs_filegroup_name(library),
    )],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in EXPERIMENTAL_LIBRARIES if library not in AD_HOC_LIBRARIES]

[cc_library(
    name = "experimental-{library}_mocks".format(library = library),
    testonly = True,
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = mocks_filegroup_name(library),
    )],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}_mocks".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in EXPERIMENTAL_LIBRARIES if library not in NO_MOCK_LIBRARIES]

[cc_library(
    name = "experimental-{library}".format(library = library),
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = hdrs_filegroup_name(library),
    )],
    deprecation = "this library is now GA, please use //:{library} instead.".format(library = library),
    tags = ["manual"],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in TRANSITION_LIBRARIES if library not in AD_HOC_LIBRARIES]

[cc_library(
    name = "experimental-{library}_mocks".format(library = library),
    testonly = True,
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = mocks_filegroup_name(library),
    )],
    deprecation = "this library is now GA, please use //:{library}_mocks instead.".format(library = library),
    tags = ["manual"],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}_mocks".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in TRANSITION_LIBRARIES if library not in NO_MOCK_LIBRARIES]

[cc_library(
    name = "{library}".format(library = library),
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = hdrs_filegroup_name(library),
    )],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in GA_LIBRARIES + TRANSITION_LIBRARIES if library not in AD_HOC_LIBRARIES]

[cc_library(
    name = "{library}_mocks".format(library = library),
    testonly = True,
    hdrs = ["//google/cloud/{dir}:{group}".format(
        dir = library_dir_name(library),
        group = mocks_filegroup_name(library),
    )],
    deps = [
        "//google/cloud/{dir}:google_cloud_cpp_{library}_mocks".format(
            dir = library_dir_name(library),
            library = library,
        ),
    ],
) for library in GA_LIBRARIES + TRANSITION_LIBRARIES if library not in NO_MOCK_LIBRARIES]

cc_library(
    name = "bigquery-mocks",
    testonly = True,
    hdrs = ["//google/cloud/bigquery:mocks"],
    deprecation = "please use //:bigquery_mocks instead.",
    tags = ["manual"],
    deps = [
        "//google/cloud/bigquery:google_cloud_cpp_bigquery_mocks",
    ],
)

cc_library(
    name = "iam-mocks",
    testonly = True,
    hdrs = ["//google/cloud/iam:mocks"],
    deprecation = "please use //:iam_mocks instead.",
    tags = ["manual"],
    deps = [
        "//google/cloud/iam:google_cloud_cpp_iam_mocks",
    ],
)

bool_flag(
    name = "enable-ctype-cord-workaround",
    build_setting_default = False,
    visibility = ["//:__subpackages__"],
)

cc_library(
    name = "storage_grpc",
    hdrs = ["//google/cloud/storage:public_grpc_hdrs"],
    deps = [
        "//google/cloud/storage:google_cloud_cpp_storage_grpc",
    ],
)

cc_library(
    name = "storage_grpc_mocks",
    testonly = True,
    hdrs = ["//google/cloud/storage:grpc_mocks_hdrs"],
    deps = [
        "//google/cloud/storage:google_cloud_cpp_storage_grpc_mocks",
    ],
)

cc_library(
    name = "experimental-storage_grpc",
    hdrs = ["//google/cloud/storage:public_grpc_hdrs"],
    deps = [
        "//google/cloud/storage:google_cloud_cpp_storage_grpc",
    ],
)

cc_library(
    name = "experimental-storage_grpc_mocks",
    testonly = True,
    hdrs = ["//google/cloud/storage:grpc_mocks_hdrs"],
    deps = [
        "//google/cloud/storage:google_cloud_cpp_storage_grpc_mocks",
    ],
)

cc_library(
    name = "common",
    hdrs = ["//google/cloud:common_hdrs"],
    deps = [
        "//google/cloud:google_cloud_cpp_common_private",
    ],
)

cc_library(
    name = "grpc_utils",
    hdrs = ["//google/cloud:grpc_utils_hdrs"],
    deps = [
        "//google/cloud:google_cloud_cpp_grpc_utils_private",
    ],
)

cc_library(
    name = "mocks",
    testonly = True,
    hdrs = ["//google/cloud:mocks"],
    deps = [
        "//google/cloud:google_cloud_cpp_mocks",
    ],
)

cc_library(
    name = "opentelemetry",
    hdrs = ["//google/cloud/opentelemetry:public_hdrs"],
    deps = [
        "//google/cloud/opentelemetry:google_cloud_cpp_opentelemetry",
    ],
)

cc_library(
    name = "compute",
    hdrs = [
        "//google/cloud/compute:{}_public_hdrs".format(l.replace("compute_", ""))
        for l in GOOGLE_CLOUD_CPP_GA_LIBRARIES
        if l.startswith("compute_")
    ],
    deps = [
        "//google/cloud/compute:google_cloud_cpp_{}".format(l)
        for l in GOOGLE_CLOUD_CPP_GA_LIBRARIES
        if l.startswith("compute_")
    ],
)

cc_library(
    name = "experimental-bigquery-rest",
    deps = [
        "//google/cloud/bigquery:google_cloud_cpp_bigquery_rest",
    ],
)

cc_library(
    name = "experimental-bigquery-rest_mocks",
    testonly = True,
    deps = [
        "//google/cloud/bigquery:google_cloud_cpp_bigquery_rest_mocks",
    ],
)

cc_library(
    name = "experimental-universe_domain",
    deps = [
        "//google/cloud:google_cloud_cpp_universe_domain",
    ],
)

bool_flag(
    name = "enable_opentelemetry",
    build_setting_default = False,
    visibility = ["//:__subpackages__"],
)
